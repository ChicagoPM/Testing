<!DOCTYPE html>
<html>
  <head>
    <title>Testing</title>
    <meta http-equiv="Content-Type" content="text/html; charset=UTF-8"/>
    <script src="remark/remark.min.js" type="text/javascript">
        {
            "highlightStyle": "solarized_light",
            "highlightLanguage": "no-highlight"
        }
    </script>
    <style type="text/css" media="screen">
/*
SOLARIZED HEX 16/8 TERMCOL XTERM/HEX L*A*B RGB HSB
--------- ------- ---- ------- ----------- ---------- ----------- -----------
base03 #002b36 8/4 brblack 234 #1c1c1c 15 -12 -12 0 43 54 193 100 21
base02 #073642 0/4 black 235 #262626 20 -12 -12 7 54 66 192 90 26
base01 #586e75 10/7 brgreen 240 #585858 45 -07 -07 88 110 117 194 25 46
base00 #657b83 11/7 bryellow 241 #626262 50 -07 -07 101 123 131 195 23 51
base0 #839496 12/6 brblue 244 #808080 60 -06 -03 131 148 150 186 13 59
base1 #93a1a1 14/4 brcyan 245 #8a8a8a 65 -05 -02 147 161 161 180 9 63
base2 #eee8d5 7/7 white 254 #e4e4e4 92 -00 10 238 232 213 44 11 93
base3 #fdf6e3 15/7 brwhite 230 #ffffd7 97 00 10 253 246 227 44 10 99
yellow #b58900 3/3 yellow 136 #af8700 60 10 65 181 137 0 45 100 71
orange #cb4b16 9/3 brred 166 #d75f00 50 50 55 203 75 22 18 89 80
red #dc322f 1/1 red 160 #d70000 50 65 45 220 50 47 1 79 86
magenta #d33682 5/5 magenta 125 #af005f 50 65 -05 211 54 130 331 74 83
violet #6c71c4 13/5 brmagenta 61 #5f5faf 50 15 -45 108 113 196 237 45 77
blue #268bd2 4/4 blue 33 #0087ff 55 -10 -45 38 139 210 205 82 82
cyan #2aa198 6/6 cyan 37 #00afaf 60 -35 -05 42 161 152 175 74 63
green #859900 2/2 green 64 #5f8700 60 -20 65 133 153 0 68 100 60
*/

body {
    font-size: 1.3em;
}

.slide .inverse {
    color: #839496; /* base0 */
    background-color: #002b36; /* base03 */
}

.slide {
    color: #657b83; /* base00 */
    background-color: #fdf6e3; /* base3 */
}

.slide pre, .slide code {
    color: #586e75; /* base01 */
    background-color: #eee8d5; /* base2 */
}

.slide .inverse pre, .slide .inverse code {
    color: #93a1a1; /* base1 */
    background-color: #073642; /* base02 */
}

a:link, a:visited, a:hover, a:active {
    color: #268bd2; /* blue */
}

.footnote {
    position: absolute;
    bottom: 3em;
}
.footnote.right {
    right: 3em;
}
.code {
    font-family: Monaco, monospace;
}
h1, h2, h3 { text-align: center; }
h5, h6 { margin: 0.5em; font-weight: normal }

    </style>
  </head>
  <body>
    <textarea id="source">
name: title
class: center, middle, inverse

# Testing Your Code

.footnote.right[Chicago.PM]
---
class: center, middle

# What? Why? How? and When?

---
class: center, middle, inverse

# What?

---
class: center, middle

# Make sure your code works

---
class: center, middle

# User testing
(make them work for it)

---
class: center, middle

# Writing code to make sure your code works

---
class: center, middle

# Unit testing
(in isolation)

---
class: center, middle

# Writing code to make sure your entire application works together

---
class: center, middle

# Integration testing
(all together now)

---
class: center, middle

# Writing code to make sure your UI works

---
class: center, middle

# UI testing
(that was obvious)

---
class: center, middle

# Have a program run your tests automatically

---
class: center, middle

# Continuous Integration
(hudson/jenkins, buildbot)

---
class: center, middle, inverse

# Why?

---
class: center, middle

# To make sure your code works

---
class: center, middle

# To make sure your code still works later

---
class: center, middle

# To make sure your code works after you change something

---
class: center, middle

# To make it easier to change something

---
class: center, middle

# To show your users that your code works

---
class: center, middle

# Remember: You're your own user

(so why are you hitting yourself?)

---
class: center, middle

# To show your boss your code works
(but your tests might have a bug)
---
class: center, middle, inverse

# How?

---
class: center, middle, inverse
# Test::More
## The Standard

---
class: middle
.center[
# Some code to test
]
```perl
# lib/MyCalc.pm
package MyCalc;

sub add {
    my ( $x, $y ) = @_;
    return $x + $y;
}

sub mult {
    my ( $x, $y ) = @_;
    return $x * $y;
}

1;
```
---
class: middle
.center[
# ok()
### Test for boolean truth
]
```perl
# t/mycalc.t
use Test::More
use MyCalc;

ok MyCalc::add( 2, 3 ) == 5;
ok MyCalc::mult( 2, 3 ) == 6;
ok MyCalc::add( 2, 2 ) == 5;

done_testing;
```
---
class: middle
# Run the test
```
$ perl t/mycalc.t
ok 1
ok 2
not ok 3
#   Failed test at t/mycalc.t line 7.
1..3
# Looks like you failed 1 test of 3.
```
---
class: middle
# done_testing
### We've run everything we've expected to run
.footnote.right[
##### We don't plan to fail
##### We fail to plan
]
---
class: middle
# is()
### Test for equality
```perl
# t/mycalc.t
use Test::More
use MyCalc;

is MyCalc::add( 2, 3 ), 5;
is MyCalc::mult( 2, 3 ), 6;
is MyCalc::add( 2, 2 ), 5;

done_testing;
```
---
class: middle
# Run the test
```
$ perl t/mycalc.t
ok 1
ok 2
not ok 3
#   Failed test at test.t line 7.
#          got: '4'
#     expected: '5'
1..3
# Looks like you failed 1 test of 3.
```
---
class: inverse, center, middle
# Test::Differences

---
class: middle
.center[
# Some code to test
]
```perl
package Lorem;

sub ipsum() {
    return &lt;&lt;LOREM;
Lorem ipsum dolor sit amet, consectetur
adipiscing elit. Nullam porta augue et
mattis egestas. Praesent mollis ultricies
felis. Nullam convallis ligula non
dui elementum porttitor. Duis ultricies
placerat tortor, vel lacinia sem
condimentum ut.
LOREM
}

sub array() {
    return split "\n", ipsum();
}

1;
```
---
class: middle
.center[
# Test a block of text
]

```perl
use Lorem;
use Test::More;
use Test::Differences;

eq_or_diff( Lorem::ipsum, &lt;&lt;LOREM );
Gollum ipsum dolor sit amet, consectetur
adipiscing elit. Nullam porta augue et
mattis egestas. Praesent mollis ultricies
felis. Nullam convallis ligula non
dui elementum porttitor. Duis ultricies
placerat tortor, vel lacinia sem
condimentum ut.
LOREM
```
---
class: middle
.center[
# Run the Test
]
```
$ perl t/lorem.t
not ok 1
#   Failed test at lorem.t line 6.
# +---+-------------------------------------------+-------------------------------------------+
# | Ln|Got                                        |Expected                                   |
# +---+-------------------------------------------+-------------------------------------------+
# *  1|'Lorem ipsum dolor sit amet, consectetur   |'Gollum ipsum dolor sit amet, consectetur  *
# |  2|adipiscing elit. Nullam porta augue et     |adipiscing elit. Nullam porta augue et     |
# |  3|mattis egestas. Praesent mollis ultricies  |mattis egestas. Praesent mollis ultricies  |
# |  4|felis. Nullam convallis ligula non         |felis. Nullam convallis ligula non         |
# |  5|dui elementum porttitor. Duis ultricies    |dui elementum porttitor. Duis ultricies    |
# |  6|placerat tortor, vel lacinia sem           |placerat tortor, vel lacinia sem           |
# |  7|condimentum ut.                            |condimentum ut.                            |
# |  8|'                                          |'                                          |
# +---+-------------------------------------------+-------------------------------------------+
1..1
# Looks like you failed 1 test of 1.
```
---
class: middle
.center[
# Test arrays of items
]

```perl
use Lorem;
use Test::More;
use Test::Differences;

eq_or_diff(
    [ Lorem::array ],
    [
        'Lorem ipsum dolor sit amet, consectetur',
        'adipiscing elit. Nullam porta augue et',
        'mattis egestas. Praesent mollis ultricies',
        'felis. Nullam convallis ligula non',
        'dui elementum porttitor. Duis ultricies',
        'placerat tortor, vel lacinia sem',
        'condimentum ut.',
    ],
);

```
---
class: middle
.center[
# Run the test
]
```
not ok 1
#   Failed test at lorem.t line 6.
# +----+---------------------------------------------+---------------------------------------------+
# | Elt|Got                                          |Expected                                     |
# +----+---------------------------------------------+---------------------------------------------+
# *   0|'Lorem ipsum dolor sit amet, consectetur'    |'Gollum ipsum dolor sit amet, consectetur'   *
# |   1|'adipiscing elit. Nullam porta augue et'     |'adipiscing elit. Nullam porta augue et'     |
# |   2|'mattis egestas. Praesent mollis ultricies'  |'mattis egestas. Praesent mollis ultricies'  |
# |   3|'felis. Nullam convallis ligula non'         |'felis. Nullam convallis ligula non'         |
# |   4|'dui elementum porttitor. Duis ultricies'    |'dui elementum porttitor. Duis ultricies'    |
# |   5|'placerat tortor, vel lacinia sem'           |'placerat tortor, vel lacinia sem'           |
# |   6|'condimentum ut.'                            |'condimentum ut.'                            |
# +----+---------------------------------------------+---------------------------------------------+
1..1
# Looks like you failed 1 tests of 1.
```
---
class: inverse, center, middle
# Test::Exception

Test life and death
---
class: middle
.center[
# Code to test
]
```perl
package Dies;

sub dies {
    die "To be or not to be!";
}

sub lives {
    "To be!";
}

sub throws {
    die bless {}, 'Shake::Spear';
}

1;
```
---
class: middle
.center[
# Tests
]
```perl
use Dies;
use Test::More;
use Test::Exception;

dies_ok { Dies::dies };
lives_ok { Dies::lives };
throws_ok { Dies::dies } qr{^To be};
throws_ok { Dies::throws } 'Shake::Spear';

done_testing;
```
---
class: middle
.center[
# Run the Tests
]
```perl
$ perl t/dies.t
ok 1
ok 2
ok 3 - threw Regexp ((?^:^To be))
ok 4 - threw Shake::Spear
1..4
```
---
class: inverse, center, middle
# Test::Deep
The Data Structure Test Module
---
class: middle
.center[
# Code To Test
]
```perl
package Simpsons;

sub names {
    return [ qw( Homer Marge ) ];
}

sub homer {
    return {
        age =&gt; 40,
        aliases =&gt; [ 'Mr. Plow', 'Max Power' ],
    }
}

sub marge {
    return {
        age =&gt; 34,
        aliases =&gt; [ ],
    }
}

sub all {
    return [ homer(), marge() ];
}

1;
```
---
class: middle
.center[
# Test Arrays
]
```perl
use Test::More;
use Test::Deep;

use Simpsons;

cmp_deeply Simpsons::names, [ 'Homer', 'Marge' ];
cmp_deeply Simpsons::names, bag( 'Marge', 'Homer' );
cmp_deeply Simpsons::names, superbagof( 'Homer' );
cmp_deeply Simpsons::names, subbagof( 'Homer', 'Marge', 'Bart' );

cmp_deeply Simpsons::names, [ 'Homer', 'Marge', 'Bart' ];
cmp_deeply Simpsons::names, [ 'Marge', 'Homer' ];

done_testing;
```
---
class: middle
.center[
# Run the Tests
]
```
$ perl simpsons.t
ok 1
ok 2
ok 3
ok 4
not ok 5
#   Failed test at simpsons.t line 12.
# Compared array length of $data
#    got : array with 2 element(s)
# expect : array with 3 element(s)
not ok 6
#   Failed test at simpsons.t line 13.
# Compared $data->[0]
#    got : 'Homer'
# expect : 'Marge'
1..6
# Looks like you failed 2 tests of 6.
```
---
class: middle
.center[
# Test Hashes
]
```perl
cmp_deeply Simpsons::homer,
    { age =&gt; 40, aliases =&gt; [ 'Mr. Plow', 'Max Power' ] };
cmp_deeply Simpsons::marge, { age =&gt; 34, aliases =&gt; [ ] };

cmp_deeply Simpsons::homer, 
    { age =&gt; 40, aliases =&gt; bag( 'Max Power', 'Mr. Plow' ) };
cmp_deeply Simpsons::marge, { age =&gt; 34, aliases =&gt; ignore() };

cmp_deeply
    Simpsons::all,
    bag(
        { age =&gt; 34, aliases =&gt; [] },
        { age =&gt; 40, aliases =&gt; [ 'Max Power' ] },
    );

# All ages must be integers
cmp_deeply
    Simpsons::all,
    array_each( superhashof( { age =&gt; re( qr(^\d+$) ) } ) );
```
---
class: middle
.center[
# Run the Tests
]
```
$ perl simpsons_hash.t
ok 1
ok 2
ok 3
ok 4
not ok 5
#   Failed test at simpsons_hash.t line 13.
# Comparing $data as a Bag
# Missing: 1 reference
# Extra: 1 reference
ok 6
1..6
# Looks like you failed 1 test of 6.
```
---
class: center, middle, inverse
# Use ALL THE MODULES
---
class: center, middle
# Test::Most

Test::More

Test::Deep

Test::Differences

Test::Exception
---
class: middle
.center[
# Remove The Boilerplate
]
```perl
use strict;
use warnings;
use Test::More;
use Test::Exception;
use Test::Deep;
use Test::Differences;
use Test::Warn;
```
```perl
use Test::Most;
```
---
class: middle
.center[
# Die on Failure
]
```perl
use Test::More 'die';
```
---
class: middle
.center[
# Test::Kit
Build your own Test::Most
]
```perl
package My::Test::Kit;

use Test::Kit qw(
    strict
    warnings
    Test::More
    Test::Deep
    Test::Exception
    Test::NoWarnings
);

1;
```
```perl
use My::Test::Kit;
```
---
class: center, middle, inverse
# Organize Your Tests
---
class: center, middle
# subtest
### Part of Test::More
---
class: middle
.center[
# Group tests together
]
```perl
use Test::More;
use MyCalc;

subtest 'add tests' =&gt; sub {
    is MyCalc::add( 2, 3 ), 5;
    is MyCalc::add( 2, 2 ), 5;
};
subtest 'mult tests' =&gt; sub {
    is MyCalc::mult( 2, 3 ), 6;
    is MyCalc::mult( 0, 4 ), 0;
};

done_testing;
```
---
class: middle
.center[
# Run the Tests
]
```
$ perl subtest.t
    ok 1
    not ok 2
    #   Failed test at subtest.t line 7.
    #          got: '4'
    #     expected: '5'
    1..2
    # Looks like you failed 1 test of 2.
not ok 1 - add tests
#   Failed test 'add tests'
#   at subtest.t line 8.
    ok 1
    ok 2
    1..2
ok 2 - mult tests
1..2
# Looks like you failed 1 test of 2.
```
---
class: center, middle
# Test::Class::Moose
### Class-based tests
---
class: center, middle
# Why Test Classes?
Tests with OO

Inherit Tests

Compose with Roles

Test Plugin Points
---
class: middle
.center[
# A Test Class
]
```perl
package TestsFor::MyCalc;

use Test::Class::Moose;
use MyCalc;

sub test_add {
    is MyCalc::add( 2, 3 ), 5;
    is MyCalc::add( 2, 2 ), 5;
}
sub test_mult {
    is MyCalc::mult( 2, 3 ), 6;
    is MyCalc::mult( 0, 4 ), 0;
}

1;
```
---
class: middle
.center[
# Run the Tests
]
```
$ perl testlib.t
1..1
#
# Running tests for TestsFor::MyCalc
#
    1..2
    # TestsFor::MyCalc-&gt;test_add()
        ok 1
        ok 2
        1..2
    ok 1 - test_add
    # TestsFor::MyCalc-&gt;test_mult()
        ok 1
        ok 2
        1..2
    ok 2 - test_mult
ok 1 - TestsFor::MyCalc
```
---
class: center, middle, inverse

# When?

---
class: center, middle

# You've already written tests!

---
class: center, middle

# Right now!

---
class: center, middle

# As Soon As Possible!

---
class: center, middle

# Before you fix a bug

---
class: center, middle

# Before you write a new feature

---
class: center, middle

# Before you start implementing anything

---
class: center, middle, inverse

# Test-Driven Development

---
class: center, middle

# Design and Test at the same time!

---
class: center, middle

# Digression: Waterfall

Big design up front
---
class: center, middle

# Modified Waterfall

Repetitive waterfall
---
class: center, middle

# Agile / Scrum

Anything can change at any time for <del>no</del>good reason
---
class: center, middle

# Having tests is Agile

---
class: center, middle

# TDD is Agile

    </textarea>
    <div id="slideshow"></div>
  </body>
</html>
